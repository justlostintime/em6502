' Gambas class file

CurrentFile As String = ""
MyScript As New ScriptManager(True, FMain.AsmDeviceDirectory)
Originaltext As String = ""

Public CurrentDeviceClass As Class = Null

Public Sub OpenDev_Click()

  Dialog.path = FMain.AsmDeviceDirectory & "/"
  Dialog.filter = ["*.dev", "Device File"]
  Dialog.AutoExt = True
  If Dialog.openFile() Then Return
  DeviceEditor.load(Dialog.path)
  CurrentFile = Dialog.Path
  Me.title = "6502 Device Editor - " & CurrentFile
  OriginalText = DeviceEditor.text

End

Public Sub NewDev_Click()

  DeviceEditor.text = File.Load("Template.dev")

End

Public Sub SAVEASDEV_Click()

  Dialog.path = FMain.AsmDeviceDirectory & "/"
  Dialog.filter = ["*.dev", "Device File"]
  Dialog.AutoExt = True
  If Dialog.SaveFile() Then Return
  DeviceEditor.Save(Dialog.path)
  CurrentFile = Dialog.Path
  Me.title = "6502 Device Editor - " & CurrentFile

End

Public Sub Savedev_Click()

  If CurrentFile = "" Then
    SAVEASDEV_Click()
    Return
  Endif

  DeviceEditor.Save(CurrentFile)

End

Public Sub Form_Close()

  If DeviceEditor.text <> OriginalText Then
    Savedev_Click()
  Endif

  Settings["devEditor/lastfile"] = CurrentFile
  Settings["devEditor/top"] = DeviceDevelopement.Top
  Settings["devEditor/left"] = DeviceDevelopement.Left
  Settings["devEditor/width"] = DeviceDevelopement.Width
  Settings["devEditor/height"] = DeviceDevelopement.Height

End

Public Sub Form_Open()

  DeviceDevelopement.Top = Settings["devEditor/top", DeviceDevelopement.Top]
  DeviceDevelopement.Left = Settings["devEditor/left", DeviceDevelopement.Left]
  DeviceDevelopement.Width = Settings["devEditor/width", DeviceDevelopement.Width]
  DeviceDevelopement.Height = Settings["devEditor/height", DeviceDevelopement.Height]
  CurrentFile = Settings["devEditor/lastfile"]
  Me.title = "6502 Device Editor - " & CurrentFile
  If currentfile <> "" Then
    DeviceEditor.Load(CurrentFile)
  Endif

End

Public Sub CutDev_Click()

  DeviceEditor.Cut()

End

Public Sub Pastedev_Click()

  DeviceEditor.Paste()

End

Public Sub Selectalldev_Click()

  DeviceEditor.SelectAll()

End


Public Sub Form_Resize()

  DeviceEditor.height = DeviceDevelopement.Height - (DeviceEditor.Top + 50)
  DeviceEditor.width = DeviceDevelopement.width - 20

End

Public Sub DoCompileMenu_Click()

  Dim DispInfo As String = "Compile Errors\n"

  If CurrentFile = "" And DeviceEditor.text = OriginalText Then
    Return
  Endif

  Savedev_Click()
  MyScript.ReturnResult = True
  Try CurrentDeviceClass = MyScript(CurrentFile)
  If Error Then
    DispInfo &= Error.text
    Dim deverrors As New InfoOut
    deverrors.title = "Errors for : " & CurrentFile
    deverrors.Show()
    Wait 0.001
    deverrors.Display(DispInfo)
    Wait 0.001
    CurrentDeviceClass = Null
  Else
    Message.Info("No Compile errors!", "OK")
  Endif

End

Public Sub DoTesting_Click()

  If CurrentDeviceClass = Null Then Return
  Dim atest As New DeviceTester
  atest.Show()
  Wait 0.001

  DeviceTester.SetDevice(CurrentFile)



End
